plugins {
    id 'com.android.application' version '8.8.0-alpha09'
    id 'org.jetbrains.kotlin.android' version '2.1.0-RC'
}

static def getEnvOrDefault(String envVar, String defaultValue) {
    def value = System.getenv(envVar)
    return value ?: defaultValue
}

def getCFApiKey = {
    String key = System.getenv("CURSEFORGE_API_KEY")
    if(key != null) return key
    File curseforgeKeyFile = new File("./curseforge_key.txt")
    if(curseforgeKeyFile.canRead() && curseforgeKeyFile.isFile()) {
        return curseforgeKeyFile.text
    }
    logger.warn('BUILD: You have no CurseForge key, the curseforge api will get disabled !')
    return "DUMMY"
}

def getApiToken = {
    getEnvOrDefault('MY_API_TOKEN', 'DUMMY')
}

def getPrivateApiToken = {
    getEnvOrDefault('MY_PRIVATE_API_TOKEN', 'DUMMY')
}

configurations {
    instrumentedClasspath {
        canBeConsumed = false
        canBeResolved = true
    }
}

android {
    namespace 'com.movtery.zalithlauncher'

    compileSdk = 35

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        releaseBuild {
            def pwd = System.getenv("MOVTERY_KEYSTORE_PASSWORD")
            storeFile file("movtery-key.jks")
            storePassword pwd
            keyAlias "mtp"
            keyPassword pwd
        }
        customDebug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    defaultConfig {
        applicationId "com.movtery.zalithlauncher"
        minSdkVersion 24
        targetSdkVersion 35
        versionCode 136500
        versionName "1.3.6.5"
        multiDexEnabled true //important
        resValue 'string', 'curseforge_api_key', getCFApiKey()
        resValue 'string', 'api_token', getApiToken()
        resValue 'string', 'private_api_token', getPrivateApiToken()
    }

    buildTypes {
        debug {
            applicationIdSuffix '.ap'
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.customDebug
            resValue 'string', 'application_package', 'com.movtery.zalithlauncher.ap'
            resValue 'string', 'storageProviderAuthorities', 'com.movtery.zalithlauncher.scoped.gamefolder.ap'
        }
        proguard {
            initWith debug
            minifyEnabled true
            shrinkResources true
        }

        release {
            // Don't set to true or java.awt will be a.a or something similar.
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // defaultConfig already set
            // multiDexEnabled = true
            // debuggable = true
            resValue 'string', 'application_package', 'com.movtery.zalithlauncher'
            resValue 'string', 'storageProviderAuthorities', 'com.movtery.zalithlauncher.scoped.gamefolder'
            signingConfig signingConfigs.releaseBuild
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def abi = output.getFilter(ABI) ?: "all"

            variant.mergeAssetsProvider.get().doLast {
                def arch = System.getProperty("arch", "all")
                def assetsDir = mergeAssetsProvider.get().outputDir.get()
                def javaVersions = ['jre-8', 'jre-17', 'jre-21']

                javaVersions.each { javaVersion ->
                    def runtimeDir = new File([assetsDir, "components/${javaVersion}"].join(File.separator))
                    fileTree(dir: runtimeDir, include: ['*.tar.xz']).each { file ->
                        if (arch != 'all' && !file.name.contains(arch) && !file.name.contains("universal")) {
                            println("delete:${file} : ${delete(file)}")
                        }
                    }
                }
            }

            def baseName = "ZalithLauncher-${variant.buildType.name == 'release' ? variant.versionName : "Debug-${variant.versionName}"}"
            outputFileName = abi == "all" ? "${baseName}.apk" : "${baseName}-${abi}.apk"
        }
    }

    splits {
        def arch = System.getProperty("arch", "all")
        if (arch != 'all') {
            abi {
                enable true
                reset()
                switch (arch) {
                    case 'arm':
                        include 'armeabi-v7a'
                        break
                    case 'arm64':
                        include 'arm64-v8a'
                        break
                    case 'x86':
                        include 'x86'
                        break
                    case 'x86_64':
                        include 'x86_64'
                        break
                }
            }
        }
    }

    ndkVersion = "26.3.11579264"

    externalNativeBuild {
        ndkBuild {
            path file('src/main/jni/Android.mk')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    bundle {
        language {
            enableSplit = false
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
        pickFirst '**/libbytehook.so'
        exclude '**/awt_headless.so'
    }

    buildFeatures {
        prefab true
    }

    buildToolsVersion = '35.0.0'
    kotlinOptions {
        jvmTarget = '21'
    }

    viewBinding {
        enabled = true
    }
}

dependencies {
    implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
    implementation 'commons-codec:commons-codec:1.17.1'
    // implementation 'com.wu-man:android-bsf-api:3.1.3'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.drawerlayout:drawerlayout:1.2.0'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation "androidx.constraintlayout:constraintlayout:2.2.0"
    implementation 'androidx.core:core-ktx:1.15.0'

    implementation 'com.github.duanhong169:checkerboarddrawable:1.0.2'
    implementation 'com.github.PojavLauncherTeam:portrait-sdp:ed33e89cbc'
    implementation 'com.github.PojavLauncherTeam:portrait-ssp:6c02fd739b'
    implementation 'com.github.Mathias-Boulay:ExtendedView:1.0.0'
    implementation 'com.github.Mathias-Boulay:android_gamepad_remapper:2.0.3'
    implementation 'com.github.Mathias-Boulay:virtual-joystick-android:2e7aa25e50'
    implementation "com.github.skydoves:powerspinner:1.2.7"
    implementation 'com.github.bumptech.glide:glide:5.0.0-rc01'
    implementation 'com.github.angcyo.DslTablayout:TabLayout:3.5.5'

    // implementation 'com.intuit.sdp:sdp-android:1.0.5'
    // implementation 'com.intuit.ssp:ssp-android:1.0.5'

    implementation 'org.tukaani:xz:1.10'
    // Our version of exp4j can be built from source at
    // https://github.com/PojavLauncherTeam/exp4j
    implementation 'net.sourceforge.htmlcleaner:htmlcleaner:2.6.1'

    // implementation 'net.sourceforge.streamsupport:streamsupport-cfuture:1.7.0'

    implementation fileTree(dir: 'libs', include: ['*'])

    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'org.commonmark:commonmark:0.24.0'
    implementation 'com.google.android.material:material:1.13.0-alpha07'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'joda-time:joda-time:2.13.0'

    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.15.0'
    implementation 'io.github.petterpx:floatingx:2.2.6'
    implementation 'org.greenrobot:eventbus:3.3.1'

    implementation('org.apache.commons:commons-text:1.12.0') {
        exclude group: 'org.apache.commons', module: 'commons-lang3'
    }
}
